#!/bin/tcsh

set odir = `cd $1; pwd` # get full output path

cp test/common.cff test/*.tpl $odir/

## Set up config file for each alignment job

@ j = 0 # job index

foreach data (`ls $2`)
  set skim = `basename $data .dat`

  cp test/${skim}TrackSelection.cff $odir/

  foreach file (`cat $data`)
    @ j++

    mkdir $odir/job$j
    replace "<FILE>" "$file" "<PATH>" $odir/job$j "<SKIM>" $skim < test/align.tpl > $odir/job$j/align.cfg
  end
end

## Set up config file for collector job

mkdir $odir/main
replace "<PATH>" $odir "<JOBS>" $j < test/collect.tpl > $odir/main/collect.cfg

## Set up config file for iteration 0

replace "<PATH>" $odir < test/initial.tpl > $odir/main/initial.cfg

## Set up config file for uploading

replace "<PATH>" $odir < test/upload.tpl > $odir/upload.cfg

## Check available disk space

set fslq = `cd $1; fs lq`
set quot = `echo $fslq | awk '{print $8}'`
set used = `echo $fslq | awk '{print $9}'`

@ f = ($quot - $used) / 1000 # free space in MB

echo Available disk space = $f MB

#!/bin/csh -f
# this script converts Madorsky's version of SP core firmware to CMSSW version
# First version March 1, 2008 DA
#

# initial stuff not in script:
echo "This script will convert Alex Madorskys SP verilog++ core"
echo "to something compatible with the conventions in CMSSW"
echo " "
echo "Assumptions:"
echo "- assumes Architecture.h is on line 1 else line 9 for SPvpp.cc"
echo "- make sure you delete vmac.h from any SPvpp header files (i.e. L1MuCSC_SPvpp_fsu.cpp)"
echo " "

# put translated files into convert/ subdirectory
rm -rf convert
mkdir convert

# defin quote
set q = '"'
# defin slash
set s = '/'
# defin lbra, rbra
set lbra = '<'
set rbra = '>'

# First take care of .cpp files:
foreach i (L1MuCSC_*.cpp)
#foreach i (L1MuCSC_SPvpp_eu2b2.cpp)
	echo "processing "$i":"
	set name = `echo $i | awk '{s = substr($1,1,length($1)-4); print s}'`
#	echo "    "$name
	set newname = `echo $name | awk '{s = substr($1,9,length($1)); print s}'`
#	echo "    newname: "$newname

	rm -f junk*

#       rename class
	sed -e "s/L1MuCSC_//" $i > junk

#       remove architecture.h, assuming it is first line
	if ( $newname != SPvpp ) then
	   sed -e "1,1 d" junk > junk2
	else 
	   sed -e "9,9 d" junk > junk2
	endif

#       change directories for includes, and put in brackets
	sed -e "s_\"$q"Trigger/L1CSCTrackFinder_\"$lbra"L1Trigger/CSCTrackFinder_" junk2 > junk3
	sed -e "/^#/s/\"$q"/\"$rbra"/" junk3 > junk4

#       change directory for vmac
	sed -e "s_L1Trigger/CSCTrackFinder/src/vmac_L1Trigger/CSCCommonTrigger/interface/vmac_" junk4 > junk5

#       copy to directory with new filename and change file extension
	cp junk5 convert/$newname.cc

end

# Now vlib.cc
foreach i (vlib.cpp)
	echo "processing "$i":"
	set name = `echo $i | awk '{s = substr($1,1,length($1)-4); print s}'`
#	echo "    "$name
	set newname = `echo $name | awk '{s = substr($1,1,length($1)); print s}'`
#	echo "    newname: "$newname

	rm -f junk*

#       change directories for includes, and put in brackets
	sed -e "/^#/s_\"$q"vlib_\"$lbra"L1Trigger/CSCCommonTrigger/interface/vlib_" $i  > junk
	sed -e "1,6 s/\"$q"/\"$rbra"/" junk > junk2

#       change directory for vmac
	sed -e "s_L1Trigger/CSCTrackFinder/src/vmac_L1Trigger/CSCCommonTrigger/interface/vmac_" junk2 > junk3

#       remove extraneous ";"
	sed -e "/^unop/ s/);/)/" junk3 > junk4
	sed -e "/^binop/ s/);/)/" junk4 > junk5
	sed -e "/^compop/ s/);/)/" junk5 > junk6
	sed -e "s/};/}/" junk6 > junk7


#       copy to directory with new filename and change file extension
	cp junk7 convert/$newname.cc

end

# Next take care of .h files:
foreach i (L1MuCSC_*.h)
#foreach i (L1MuCSC_SPvpp.h)
	echo "processing "$i":"
	set name = `echo $i | awk '{s = substr($1,1,length($1)-2); print s}'`
#	echo "    "$name
	set newname = `echo $name | awk '{s = substr($1,9,length($1)); print s}'`
#	echo "    newname: "$newname

	rm -f junk*

#       rename class
	sed -e "s/L1MuCSC_//" $i > junk

#       change directories for includes, and put in brackets
	sed -e "s_\"$q"Trigger/L1CSCTrackFinder_\"$lbra"L1Trigger/CSCTrackFinder_" junk > junk2
	sed -e "/^#/s/\"$q"/\"$rbra"/" junk2 > junk3

#       change directory for vlib
	sed -e "s_L1Trigger/CSCTrackFinder/src/vlib_L1Trigger/CSCCommonTrigger/interface/vlib_" junk3 > junk4

#       now make sure header files wrapped in ifndef
	set chk = `grep ifndef junk4`
	set add = `echo $chk | awk '{s = substr($1,2,7); print s}'`
	if ( $add != ifndef ) then
	   cp junk4 junk5
	   echo "#ifndef _FILE_"$newname"_FILE_" > junkH
	   echo "#define _FILE_"$newname"_FILE_" >> junkH
	   echo "#endif" > junkT
	   cat junkH junk5 junkT > junk4
	endif

#       copy to directory with new filename
	cp junk4 convert/$newname.h

end

# now other cases

foreach i (vlib.h)
	echo "processing "$i":"
	set name = `echo $i | awk '{s = substr($1,1,length($1)-2); print s}'`
#	echo "    "$name
	set newname = `echo $name | awk '{s = substr($1,1,length($1)); print s}'`
#	echo "    newname: "$newname

	rm -f junk*
	cp $i junk

#       now make sure RVALS defined
	set chk = `grep "define RVALS" $i`
	set add = `echo $chk | awk '{s = substr($1,2,7); print s}'`
	if ( $add != define ) then
	   cp junk junk2
	   echo "#ifndef RVALS" > junkH
	   echo "#define RVALS 2 // max bits in reg/wire divided by 32" >> junkH
	   echo "#endif" >>junkH
	   cat junkH junk2 > junk
	endif

#       copy to directory with new filename
	cp junk convert/$newname.h

end

# do nothing
cp spbits.h convert/
cp vmac.h convert/



echo " "
echo "Everything copied to convert/"
echo "- Add ';' to end of line 25 of vlib.cc"
echo "- Now move vmac.h to L1Trigger/CSCCommonTrigger/interface/vmac.h"
echo "- Now move vlib.h to L1Trigger/CSCCommonTrigger/interface/vlib.h"
echo "- Now move vlib.cc to L1Trigger/CSCCommonTrigger/src/vlib.cc"
